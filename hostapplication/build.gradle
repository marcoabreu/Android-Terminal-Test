apply plugin: 'application'
apply plugin: 'java'

version = '1.0'
targetCompatibility = 1.8
sourceCompatibility = 1.8

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':terminalcommunication')

    compile 'com.github.marcoabreu:jadb:b2ab545'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.6.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'

    compile 'com.intellij:forms_rt:6.0.5' //IntelliJ UI Designer lib

    //TODO: Remove, these are script-related libraries and will be supplied by them
    compile group: 'org.scream3r', name: 'jssc', version: '2.8.0'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'com.marcoabreu.att.host.HostApp'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyJarToBin(type: Copy) {
    from fatJar
    into "../build/release"
}

task copyScripts(type: Copy) {
    from 'res/scripts'
    into "../build/release/scripts"
}

task copyConfig(type: Copy) {
    from 'res/config'
    into "../build/release/config"
}

task buildHostApp (dependsOn: [fatJar, copyJarToBin, copyScripts, copyConfig])